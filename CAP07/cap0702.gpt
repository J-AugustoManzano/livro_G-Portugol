algoritmo CALCULADORA; // (cap0902.gpt)

variáveis
  R, A, B : real;
  OPCAO : inteiro;
fim-variáveis

// {Trecho principal do programa}
início
  OPCAO := 0;
  enquanto (OPCAO <> 5) faça
    imprima("1 - Adicao");
    imprima("2 - Subtracao");
    imprima("3 - Multiplicacao");
    imprima("4 - Divisao");
    imprima("5 - Fim de Programa");
    imprima("Escolha uma opcao:");
    OPCAO := leia();
    se (OPCAO <> 5) então
      se (OPCAO = 1) então
        ROTCALC('+');
      fim-se
      se (OPCAO = 2) então
        ROTCALC('-');
      fim-se
      se (OPCAO = 3) então
        ROTCALC('*');
      fim-se
      se (OPCAO = 4) então
        ROTCALC('/');
      fim-se
      se (OPCAO < 1) ou (OPCAO > 5) então 
        imprima("Opcao invalida - Tente novamente.");
      fim-se
    fim-se
  fim-enquanto
fim
// {Trecho de sub-rotinas de entrada e saída}

função ENTRADA()
início
  imprima(" "); 
  imprima("Entre o 1o. valor:"); 
  A := leia();
  imprima("Entre o 2o. valor:"); 
  B := leia();
fim

função SAIDA()
início
  imprima(" "); 
  imprima("O resultado da operacao equivale a: ", R, ".");
  imprima(" "); 
fim

// {Trecho com função para o cálculo das operações}

função CALCULO(X : real, Y : real, OPERADOR : caractere) : real
início
  se (OPERADOR = '+') então
    retorne X + Y;
  fim-se
  se (OPERADOR = '-') então
    retorne X - Y;
  fim-se
  se (OPERADOR = '*') então
    retorne X * Y;
  fim-se
  se (OPERADOR = '/') então
    retorne X / Y;
  fim-se
fim
// {Trecho com sub-rotina geral}
função ROTCALC(OPERACAO : caractere)
início
  se (OPERACAO = '+') então
    imprima("Rotina de Adicao");
  fim-se
  se (OPERACAO = '-') então
    imprima("Rotina de Subtracao");
  fim-se
  se (OPERACAO = '*') então
    imprima("Rotina de Multiplicacao");
  fim-se
  se (OPERACAO = '/') então
    imprima("Rotina de Divisao");
  fim-se
  ENTRADA();
  se (OPERACAO = '/') então
    se (B = 0) então
      imprima(" "); 
      imprima("O resultado da operacao equivale a: ERRO.");
      imprima(" "); 
    senão
      R := CALCULO(A, B, '/');
      SAIDA();
    fim-se
  fim-se
  se não (OPERACAO = '/') então
    R := CALCULO(A, B, OPERACAO);
    SAIDA();
  fim-se
fim
